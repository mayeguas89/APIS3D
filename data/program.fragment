#version 330
#extension GL_NV_shadow_samplers_cube : enable
#extension GL_ARB_explicit_uniform_location : enable

// Lights

layout(location = 1) uniform int computeLight;
// Color de la luz ambiente
layout(location = 2) uniform vec3 ambient;
layout(location = 3) uniform float ambientIntensity;

// Brillo del material
layout(location = 4) uniform int shininess;
layout(location = 5) uniform float alpha;

// CameraPosition
layout(location = 6) uniform vec3 cameraPosition;

// Texture
layout(location = 7) uniform int hasTexture;
layout(location = 8) uniform int isCubeMap;
layout(location = 9) uniform sampler2D colorTexture;
layout(location = 10) uniform samplerCube cubeTexture;
layout(location = 11) uniform sampler2D normalTexture;
layout(location = 12) uniform int computeReflection;
layout(location = 13) uniform int computeRefraction;
layout(location = 14) uniform float refractCoef;
layout(location = 15) uniform int computeNormalTexture;
layout(location = 16) uniform sampler2D shadowTexture;
layout(location = 17) uniform int computeShadows;
layout(location = 18) uniform vec3 shadowLightDirection;

#define MAX_NUMBER_OF_LIGHTS 8
struct LightSource
{
    int IsActive;
    int Type;
    vec3 Position;
    vec4 Direction;
    float ConstantAttenuation;
    float LinearAttenuation;
    float QuadraticAttenuation;
    vec3 Color;
    // Para spotLights cos(phi)
    float Cutoff;
    float AmbientContribution;
    float DifuseContribution;
    float SpecularContribution;
};
layout(location = 19) uniform LightSource Lights[MAX_NUMBER_OF_LIGHTS];


// Input
in vec4 fColor;
in vec2 fTextCoord;
in vec4 fNormal;
in vec4 fPos;
in mat3 TBN;
in vec4 fShadowCoord;

// Output
out vec4 FragColor;

void main() {

    vec3 fTextCube = fPos.xyz;
    float visibility = 1.0f;
    vec4 baseColor = fColor;

    if(computeReflection == 1)
    {
        vec3 I = vec3(normalize(fPos.xyz - cameraPosition));
        fTextCube = reflect(I, vec3(normalize(fNormal)));
    }
    else if(computeRefraction == 1)
    {
        vec3 I = vec3(normalize(fPos.xyz - cameraPosition));
        fTextCube = refract(I, normalize(fNormal.xyz), refractCoef);
    }

    vec3 normal = normalize(fNormal.xyz);
    
    if(computeNormalTexture == 1)
    {
        vec3 normalTextValue = texture2D(normalTexture, fTextCoord).xyz;
        normalTextValue = (normalTextValue*2.0f)-1.f;
        normal = -TBN * normalTextValue;
    }

    
    if(computeShadows == 1)
    {
        float bias = 0.0005f;

        //float bias = max(0.005f * (1.0f - dot(normal, normalize(shadowLightDirection))), 0.0005f);
        //if(texture2D(shadowTexture, fShadowCoord.xy).z < fShadowCoord.z - 0.0005f)
        //{
        //    visibility = 0.5;
        //}

        // PCF 3 steps
        float shadow = 0.0;
        vec2 texelSize = 1.0 / textureSize(shadowTexture, 0);
        for(int x = -1; x <= 1; ++x)
        {
            for(int y = -1; y <= 1; ++y)
            {
                float pcfDepth = texture2D(shadowTexture, fShadowCoord.xy + vec2(x, y) * texelSize).r; 
                shadow += fShadowCoord.z - bias > pcfDepth ? 1.0 : 0.0;        
            }    
        }
        shadow /= 9.0;
        visibility = visibility*(1-shadow);

    }

    if(hasTexture == 1)
    {
        if(isCubeMap == 1)
        {
            baseColor = vec4(textureCube(cubeTexture, fTextCube).rgb, 1.f);   
        }
        else
        {
            baseColor = texture2D(colorTexture, fTextCoord);   
        }
    }

    if (computeLight == 1)
    {
        vec3 totalLight = vec3(0.f);
        for (int i=0;i<MAX_NUMBER_OF_LIGHTS;i++)
        {
            if(Lights[i].IsActive ==  0)
                continue;
            vec3 L = vec3(1.f);

            // Directional
            if (Lights[i].Type == 0)
            {
                L = normalize(-Lights[i].Direction.xyz);
            }
            // Point light
            else if (Lights[i].Type == 1)
            {
                L = normalize(Lights[i].Position-fPos.xyz);
            }
            // Spot light
            else if (Lights[i].Type == 2)
            {
                // Vector pointing from light to framgent position
                L = normalize(Lights[i].Position-fPos.xyz);
                // Calculate the cos of the angle between the light direction and L
                float cos_theta = max(dot(normalize(Lights[i].Direction.xyz), L), 0.0);
                // If the fragment does not received light from the focal light, give only ambient
                if (cos_theta < Lights[i].Cutoff)
                {
                    vec3 light = ambient * Lights[i].AmbientContribution * ambientIntensity * Lights[i].Color;
                    totalLight += light;
                    continue;
                }
            }

            // attenuation
            float distance = length(Lights[i].Position-fPos.xyz);
            float attenuation = Lights[i].ConstantAttenuation;
            attenuation += Lights[i].LinearAttenuation * distance;
            attenuation += Lights[i].QuadraticAttenuation * distance * distance;
            attenuation = 1.0 / (attenuation);

            vec3 N = normal;

            // Componente difusa
            float diffuse = max(dot(L, N), 0.f);

            // Componente especular
            // Vector reflexion de la luz (espejo)
            vec3 R = normalize(reflect(L, N));

            vec3 EYE = normalize(fPos.xyz - cameraPosition);
            float specular = pow(max(dot(R, EYE), 0.f), shininess);

            vec3 light = ambient * Lights[i].AmbientContribution * ambientIntensity;
            light += diffuse * Lights[i].DifuseContribution * attenuation * visibility;
            light += specular * Lights[i].SpecularContribution * attenuation * visibility;
            light *= Lights[i].Color;
            totalLight += light;
        }
        FragColor = vec4(totalLight, 1.0f) * baseColor;
        FragColor.a = FragColor.a * alpha;
    }
    else
    {
        FragColor = baseColor;
        FragColor.a = FragColor.a * 1.f;
    }

}